#extension GL_OES_EGL_image_external : require
precision highp float;
uniform samplerExternalOES uTexture;
varying vec2 texturePos;
uniform float uTime;
uniform float inAlpha;
const float PI = 3.1415926;
//毛刺
float rand(float n) {
    return fract(sin(n) * 43758.5453123);
}
void main() {
    float maxJitter = 0.06;
    float duration = 0.3;
    float colorROffset = 0.01;
    float colorBOffset = -0.025;

    float time = mod(uTime, duration * 2.0);
    float amplitude = max(sin(time * (PI / duration)), 0.0);

    float jitter = rand(texturePos.y) * 2.0 - 1.0; // -1~1
    bool needOffset = abs(jitter) < maxJitter * amplitude;

    float textureX = texturePos.x + (needOffset ? jitter : (jitter * amplitude * 0.006));
    vec2 textureCoords = vec2(textureX, texturePos.y);

    vec4 mask = texture2D(uTexture, textureCoords);
    vec4 maskR = texture2D(uTexture, textureCoords + vec2(colorROffset * amplitude, 0.0));
    vec4 maskB = texture2D(uTexture, textureCoords + vec2(colorBOffset * amplitude, 0.0));

    gl_FragColor = vec4(maskR.r, mask.g, maskB.b, inAlpha);
}